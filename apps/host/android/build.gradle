buildscript {
    ext {
        buildToolsVersion = "34.0.0"
        minSdkVersion = 23
        compileSdkVersion = 34
        targetSdkVersion = 34
        ndkVersion = "26.1.10909125"
        kotlinVersion = "1.9.22"
    }
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath("com.android.tools.build:gradle:8.1.4")
        classpath("com.facebook.react:react-native-gradle-plugin")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin")
    }
}

subprojects {
    afterEvaluate { project ->
        if (project.hasProperty("android")) {
            project.android {
                compileOptions {
                    sourceCompatibility JavaVersion.VERSION_17
                    targetCompatibility JavaVersion.VERSION_17
                }
                // Fix for older libraries missing namespace declaration
                if (namespace == null) {
                    namespace = project.group.toString()
                }
            }
        }
        project.tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
            kotlinOptions {
                jvmTarget = "17"
            }
        }
    }
}

// Fix namespace for specific libraries that don't have it
subprojects { subproject ->
    subproject.afterEvaluate {
        if (subproject.name == "react-native-network-info") {
            subproject.android.namespace = "com.pusherman.networkinfo"
        }
        if (subproject.name == "react-native-tcp-socket") {
            if (subproject.android.namespace == null) {
                subproject.android.namespace = "com.asterinet.react.tcpsocket"
            }
        }
    }
}
